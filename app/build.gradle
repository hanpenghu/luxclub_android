import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

// bugly 符号表配置
apply plugin: 'bugly'
bugly {
    appId = '91e039abbd' // 注册时分配的App ID
    appKey = '8e2bf069-5b9c-429f-89f0-0128bbb18e9e' // 注册时分配的App Key
}

android {
    compileSdkVersion 25
    buildToolsVersion rootProject.ext.buildToolsVersion

    // 打包时的时间
    def appBuildTime = new SimpleDateFormat("yyyy-MM-dd_HH-mm").format(new Date());
    //=========================可能需要修改的配置 begin=======================
    //打包时，apk的输出路径
//    def buildRoot = "C:/android_release/luxclub";
    def buildRoot = "/Users/liuchunchen/document/android_release/luxclub";
    //app的名字
    def appName = "luxclub";
    //app的包名
    def appId = "com.hsbank.luxclub"
    defaultConfig {
        applicationId appId
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 20
        versionName "1.5.0"
        flavorDimensions "versionCode"
        //solve 65K methods dex limit
        multiDexEnabled true
        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a' , 'x86', 'x86_64'
        }
//        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
//        vectorDrawables.useSupportLibrary= true
    }
    //=========================可能需要修改的配置 end=========================

    //打包时，不包含如下文件
    packagingOptions {
        exclude "META-INF/NOTICE.txt"
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/DEPENDENCIES.txt"
    }
    //lint配置
    lintOptions {
        //禁用Lint报错
        abortOnError false
    }

    //签名配置
    signingConfigs {
        debug {
            keyAlias 'luxclub'
            keyPassword 'hsbank'
            storeFile file('../debug_keystore.jks')
            storePassword 'hsbank'
        }
        release {
            storeFile file(RELEASE_STOREFILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    //编译选项
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    //构建类型
    buildTypes {
        debug {
            // 调试标志(从BuildConfig中获取)
            debuggable true
            minifyEnabled false
            zipAlignEnabled false
            // debug 模式applicationId后缀
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField "boolean", "MODE_FAST", "false"
            // 调试标志(从BuildConfig中获取)
            debuggable false
            // 是否进行混淆
            minifyEnabled true
            // 移除无用的resource文件
            shrinkResources true
            zipAlignEnabled true
            // 混淆文件的位置
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    //发布渠道配置参数
    productFlavors {
        product {
            buildConfigField("boolean", "MODE_FAST", "false")
        }
        env_test {
            buildConfigField("boolean", "MODE_FAST", "false")
        }
        env_fake {
            buildConfigField("boolean", "MODE_FAST", "true")
        }
        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [APP_CHANNEL: name, BaiduMobAd_CHANNEL:name]
        }
    }
    // 打包发布的应用名称和位置
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def appVersionName = variant.versionName;
            def appChannel = variant.flavorName;
            def appBuildType = variant.buildType.name;
            def flag = "_";
            def newFileName =
                            "${appName}${flag}" +
                            "${appVersionName}${flag}" +
                            "${appBuildType}${flag}" +
                                    "${appChannel}${flag}" +
                            "${appBuildTime}" +
                            ".apk";
            if (appBuildType.equals("release")) {
                if (output.outputFile != null && output.outputFile.name.endsWith('.apk')
                        && 'release'.equals(appBuildType)) {
//                    def apkFile = new File(buildRoot, newFileName)
                    outputFileName = newFileName
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile project(':util')
    compile 'com.tencent.bugly:crashreport:2.6.5'
    //其中latest.release指代最新Bugly SDK版本号，也可以指定明确的版本号，例如2.1.9
    compile 'com.tencent.bugly:nativecrashreport:3.3.1'
    //其中latest.release指代最新Bugly NDK版本号，也可以指定明确的版本号，例如3.0
    // android 官方设计控件库
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    // Android 卡片布局，用于listview item
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    // Google新增的比例布局，鸿洋进行了扩展
    compile 'com.zhy:percent-support-extends:1.1.1'
    // 知名图片缩放库
    compile 'com.commit451:PhotoView:1.2.5'
    compile project(':util_push_xg')
    // 性能好，拓展性强的下拉刷新组件
    compile "com.github.Aspsine:SwipeToLoadLayout:1.0.3"
    // 通用的Adapter和Viewholder，适用于Listview和RecyclerView
    compile "com.zhy:base-adapter:3.0.3"
    // retrofit
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.2.0'
    // rxjava
    compile 'io.reactivex:rxjava:1.2.1'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    // okhttp3
    compile 'com.squareup.okhttp3:okhttp:3.4.2'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.2'
    // viewpager指示器
    compile 'me.relex:circleindicator:1.2.2@aar'
    // MagicIndicator
    compile 'com.github.hackware1993:MagicIndicator:1.5.0'
    //用于界面测试
    compile 'com.hulab.android:debugkit:1.1.0'
    androidTestCompile 'junit:junit:4.12'
    compile 'junit:junit:4.12'
//    androidTestImplementation 'com.android.support.test:runner:1.0.1'
//    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    compile 'org.jetbrains:annotations-java5:15.0'
}
